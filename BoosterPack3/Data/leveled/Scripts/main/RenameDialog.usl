class CRenameDialog inherit CFrameWindow

	export constructor(string p_sDefName)
		if(!InitFromResource("main/RenameDialog","RenameDialog"))then
			CLevelEditorWrap.LogError("UI.RenameDialog","RenameDialog InitFromRessource() failed");
			return;
		endif;
		var ^CButton pxBtnOK=cast<CButton>(GetControl("ButtonOK"));
		pxBtnOK^.m_xOnClick=OnClickOK;
		var ^CButton pxBtnCancel=cast<CButton>(GetControl("ButtonCancel"));
		pxBtnCancel^.m_xOnClick=OnClickCancel;
		m_pxName=cast<CEdit>(GetControl("EditName"));
		m_pxName^.SetText(p_sDefName);
		SetDefButtonOk(pxBtnOK);
		SetDefButtonCancel(pxBtnCancel);
	endconstructor;	
	
	export destructor()
		CWindowMgr.Get().ReleaseModal(this);
	enddestructor;	
	
	export proc void Show()
		var ^CWindowMgr pxWM=^(CWindowMgr.Get());
		var ^CDesktop pxDesktop=CClientWrap.GetDesktop();
		var point xPos;
		xPos.SetX((pxDesktop^.GetWidth()-GetWidth())/2);
		xPos.SetY((pxDesktop^.GetHeight()-GetHeight())/2);
		SetPos(xPos);
		pxDesktop^.AddChild(this);			
		pxWM^.BringWindowToTop(this, true);
		pxWM^.SetModal(this);
		CWindowMgr.Get().BringWindowToTop(m_pxName,true);
	endproc;
	
	export proc bool OnClickOK()
		var string sName=m_pxName^.GetText();
		if(!m_xOnOK.IsNull())then
			m_xOnOK.Call(sName);
		endif;
		Destroy();
		return true;
	endproc;		
	proc bool OnClickCancel()
		if(!m_xOnCancel.IsNull())then
			m_xOnCancel.Call();
		endif;
		Destroy();
		return true;
	endproc;	
	
	export var procref<void,string> m_xOnOK;
	export var procref<void> m_xOnCancel;
	var ^CEdit	m_pxName;		
endclass;