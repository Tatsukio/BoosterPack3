class CViewObjGoatGraphicObj inherit CEnhancedFrameWindow
    export var ^CViewObjGoat 	m_pxParent;
    var ^CDirList       			m_pxDirList;
    var ^CRadioButton   		m_pxCheckMain;
    var ^CRadioButton   		m_pxCheckAnim;

    export var ^CEdit   			m_pxEditObjectName;
    export var ^CEdit   			m_pxEditMain;
    export var ^CList   			m_pxAnimList;

    var CGraphicObject  			m_xGraphicObj;

	export constructor()
		if(!InitFromResource("objects/ViewObjGoatGraphicObj","ObjGoatGraphicObj")) then
		    CLevelEditorWrap.LogError("UI.ViewObjGoatGraphicObj","InitFromResource failed");
			return;
		endif;

        m_xGraphicObj.m_sObjectName         = "";
        m_xGraphicObj.m_sGraphicFileValue   = "";
        m_xGraphicObj.m_asAnimFilesName     = 0;
        m_xGraphicObj.m_asAnimFilesValue    = 0;

    	m_pxEditObjectName = cast<CEdit>(GetControl("CObjectName"));
    	m_pxEditObjectName^.SetText(CLocalizer.Get().Translate("_ViewObjGoatGraphicObj_CObjectName"));
    	m_pxCheckMain = cast<CRadioButton>(GetControl("CCheckMain"));
    	m_pxCheckMain^.SetText(CLocalizer.Get().Translate("_ViewObjGoatGraphicObj_CCheckMain"));
    	m_pxCheckAnim = cast<CRadioButton>(GetControl("CCheckAnim"));
    	m_pxCheckAnim^.SetText(CLocalizer.Get().Translate("_ViewObjGoatGraphicObj_CCheckAnim"));
    	m_pxEditMain = cast<CEdit>(GetControl("CMainFileName"));
    	var ^CButton pxAddBut = cast<CButton>(GetControl("CButFileNameAdd"));
    	pxAddBut^.SetText(CLocalizer.Get().Translate("_ViewObjGoatGraphicObj_CButFileNameAdd"));
    	pxAddBut^.m_xOnClick = OnAdd;
    	var ^CButton pxRemBut = cast<CButton>(GetControl("CButFileNameRem"));
    	pxRemBut^.SetText(CLocalizer.Get().Translate("_ViewObjGoatGraphicObj_CButFileNameRem"));
    	pxRemBut^.m_xOnClick = OnRemove;
    	var ^CButton pxAcceptBut = cast<CButton>(GetControl("CButAccept"));
    	pxAcceptBut^.SetText(CLocalizer.Get().Translate("_ViewObjGoatGraphicObj_CButAccept"));
    	pxAcceptBut^.m_xOnClick = OnAccept;

        m_pxAnimList = cast<CList>(GetControl("CAnimList"));
        m_pxDirList = cast<CDirList>(GetControl("CSelectionDirList"));
        m_pxDirList^.SetFilterRule("GOD Files","*.GOD; *.god; *.God");

    	LoadPosition("ObjGoatGraphicObj");
	endconstructor;

	proc bool OnAdd()
        var string sTmp = m_pxDirList^.GetSelectedItemAsString();
		var int i = sTmp.FindRev('.');

		if (i != -1) then
    		if (m_pxCheckMain^.GetSelected()) then
    		    m_pxEditMain^.SetText(m_pxDirList^.GetSelectedItemAsString());
    		    m_xGraphicObj.m_sGraphicFileValue = sTmp;
    		else
    		    if (m_pxAnimList^.FindItem(sTmp) == -1) then
    		        m_pxAnimList^.AddItem(sTmp);
    		        m_xGraphicObj.m_asAnimFilesValue.AddEntry(sTmp);
    		    endif;
    		endif;
    	endif;

		return(true);
	endproc;

	proc bool OnRemove()
	    var string sTmp = m_pxAnimList^.GetSelectedItemAsString();

	    if (sTmp.GetLength()>1) then
	        m_xGraphicObj.m_asAnimFilesValue.DeleteEntry(m_xGraphicObj.m_asAnimFilesValue.FindEntry(sTmp));
	        m_pxAnimList^.DeleteItem(m_pxAnimList^.GetSelectedItem());
	    endif;

		return(true);
	endproc;

	proc bool OnAccept()
	    m_xGraphicObj.m_sObjectName = m_pxEditObjectName^.GetText();
	    m_pxParent^.m_pxVOGGraS^.m_pxGraphicObjMgr^.AddItem(m_xGraphicObj);
		SetVisible(false);
		return(true);
	endproc;
endclass;